window: 800 480

/*map: {
	path: to 0 0 line 800 0  800 480  0 480

	fill: rgba 0 0 0 0

	value: bigimage "map" x from -180 to 180 y from 0 to 1
}*/

turnsignal: class {
	path:
		line -15 90  15 90  15 60  30 60  0 30  -30 60  -15 60  close
		to -240 0 line 0 200  240 0

	edge: 3px rgb 1 1 1
	fill: rgba 0 0 0 0.7

	pressed: state {
		edge: 6px rgb 1 1 1
		fill: rgba 0.133 0.243 0.573 0.3

		on press: default turn_stop()
	}

	on press: pressed turn()
}

leftturnsignal: turnsignal {
	translate: 0 240
	rotate: -90

	pressed: state {
		on press: default turn_left_stop()
	}

	on press: pressed turn_left()
}

rightturnsignal: turnsignal {
	translate: 800 240
	rotate: 90

	pressed: state {
		on press: default turn_right_stop()
	}

	on press: pressed turn_right()
}

highbeams: {
	path: line 0 0  200 0  200 200  0 200 close

	edge: 2px rgb 1 1 1
	fill:  rgb 0 0 0

	translate: 300 200

	high: state {
		edge: 5px rgb 1 1 1
		fill: rgba 0.133 0.243 0.573 0.3

		on press: default highbeams_off()
	}

	on press: high highbeams_on()
}

wiper: {
	path:
		line 0 0  -45 45
		to -55 50
			line 55 50
//			qbezier 0 100  55 50
			line 25 5
			line -25 5
//			qbezier 0 25  -25 5
			close

	edge: 2px rgb 1 1 1
	fill: rgba 0 0 0 0.3

	translate: 160 10

	enabled: state {
		edge: 3px rgb 1 1 1
		fill: rgba 0.133 0.243 0.573 0.3

		scale: -1 1

		on press: default wiper_off()
	}

	on press: enabled wiper_on()
}

horn: {
	path:
		to 36 35
			qbezier -48 35  -48 60
			line -48 0
			qbezier -48 25   36 25
			close
/*		to 0 0
			qbezier  24  0   24 12
			qbezier  24 24    0 24
			qbezier -24 24  -24 12
			qbezier -24  0    0  0*/

	edge: 2px rgb 1 1 1

	translate: 650 20

	enabled: state {
		edge: 3px rgb 1 1 1
		fill: rgba 0.133 0.243 0.573 0.3

		after 1 seconds: default horn_off()
		on press: default horn_off()
		on release: default horn_off()
	}

	on press: enabled horn_on()
}

speedometer: {
	notch: class {
		path: line -160 -2  -160 2  -150 2  -150 -2 close

		fill: rgb 1 1 1
	}

	translate: 400 20

	scale: 1 -1

	s0:  notch { rotate:   0 }
	s3:  notch { rotate:  18 }
	s6:  notch { rotate:  36 }
	s9:  notch { rotate:  54 }
	s12: notch { rotate:  72 }
	s15: notch { rotate:  90 }
	s18: notch { rotate: 108 }
	s21: notch { rotate: 126 }
	s24: notch { rotate: 144 }
	s27: notch { rotate: 162 }
	s30: notch { rotate: 180 }

	// Clears the background for needle and speed
	backdrop: {
		path:
			line -150 -5
			-150 0
				-143 46.4  -121 88.2  -88.2 121  -46.4 143
			0 150
				46.4 143  88.2 121  121 88.2  143 46.4
			150 0  150 -5 close

		scale: 1 -1

		on press: default start_race()
	}

	needle: {
		path: line 10 5  -150 0  10 -5 close

		fill: rgb 1 0 0

		value: rotate from 0 at 0 to 180 at 30
	}

	timer: {
		scale: 1 -1
		translate: -55 75

		scale: 0.25

		fill: rgb 1 1 1

		value: printf "%2.0f:%02.0f.%02.0f"
	}

	speed: {
		scale: 1 -1
		translate: -55 50

		scale: 0.25

		fill: rgb 1 1 1

		value: printf "%.1f mph"
	}
}

exit: {
	path:
		to -1  1 line 1 -1
		to -1 -1 line 1  1

	edge: 0.1px rgb 1 1 1

	translate: 400 460
	scale: 20

	on press: default exit()
}

